apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

ext {
    retrofit_version = '2.5.0'
    okhttp3_version = '3.13.1'
    koin_version = '1.0.2'

    bintrayRepo = 'maven' // your repo name
    bintrayName = 'koin-retrofit' // has to be same as your library module name
    publishedGroupId = 'se.holmberg.koin-retrofit' // your module package name
    libraryName = 'koin-retrofit'
    artifact = 'koin-retrofit' // has to be same as your library module name
    libraryDescription = 'A library to help out with koin and retrofit'
    siteUrl = 'https://github.com/androidCode/time-utils'
    gitUrl = 'https://github.com/androidCode/time-utils.git'
    libraryVersion = '0.1.0'
    developerId = 'antonholmberg'
    developerName = 'Anton Holmberg'
    developerEmail = 'anton.holmberg@protonmail.com'
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

    buildVersionCode = {
        //The rules is as follows:
        //-SNAPSHOT counts as 0
        //-RC* counts as the RC number, i.e. 1 to 98
        //final release counts as 99.
        //Thus you can only have 98 Release Candidates, which ought to be enough for everyone

        def candidate = "99"
        def (major, minor, patch) = libraryVersion.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]","")
        } else {
            def rc
            (patch, rc) = patch.tokenize("rc")
            if (rc) {
                candidate = rc
            }
        }

        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect{it.toInteger()}

        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate;
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode buildVersionCode()
        versionName libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.okhttp3:okhttp:${okhttp3_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "org.koin:koin-core-ext:${koin_version}"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

apply from: 'https://raw.githubusercontent.com/numetriclabz/jcenter/master/installv.gradle'
apply from: 'https://raw.githubusercontent.com/numetriclabz/jcenter/master/bintrayv.gradle'
